#!/usr/bin/env perl

use Modern::Perl;
use AnyEvent::Twitter::Stream;

use lib qw( . );
use Twitter::Filter;

binmode STDOUT, ":utf8";
$| = 1;



my $user = shift
    or die "Usage: userstream <user>";

my $filter = Twitter::Filter->new(
        mode        => 'collect',
        application => 'userstream',
        user        => $user,
    );
    
    

my $done     = AE::cv;
my $streamer = AnyEvent::Twitter::Stream->new(
    consumer_key    => $filter->get_config( 'consumer_key' ),
    consumer_secret => $filter->get_config( 'consumer_secret' ),
    token           => $filter->get_config( 'token',        "user $user" ),
    token_secret    => $filter->get_config( 'token_secret', "user $user" ),
    method          => 'userstream',
    
    on_tweet   => \&receive_tweet,
    on_friends => \&receive_friends_list,
    on_event   => \&receive_event,
    on_error   => \&receive_error,
    on_eof     => \&receive_eof,
);
$done->recv;
exit;



sub receive_tweet {
    my $tweet = shift;
    
    # deleted tweet
    if ( defined $tweet->{'delete'}{'status'} ) {
        $filter->delete_tweet( $tweet->{'delete'}{'status'}{'id'} );
        print 'X';
    }
    else {
        $filter->save_tweet( $tweet );
        print '.';
    }
}
sub receive_friends_list {
    my $list = shift;
    say "\n-> GOT FRIENDS LIST";
}
sub receive_event {
    my $event = shift;
    
    if ( $event->{'event'} eq 'favorite' ) {
        my $favouriter = $event->{'source'}{'id'};
        my $tweet_id   = $event->{'target_object'}{'id'};
        
        $filter->mark_tweet_as_favourited( $tweet_id, $favouriter );
    }
    elsif ( $event->{'event'} eq 'list_member_added' ) {
        # say "\n-> ADDED A USER TO A LIST";
        print "%";
    }
    elsif ( $event->{'event'} eq 'follow' ) {
        # say "\n-> STARTED FOLLOWING SOMEONE";
        print "+";
    }
    else {
        say "\n-> GOT SOME OTHER EVENT";
        use Data::Dumper::Concise;
        print Dumper $event;
    }
}
sub receive_error {
    my $error = shift;
    warn "\nERROR: $error";
    $done->send;
}
sub receive_eof {
    $done->send;
    say "\n-> EOF";
}
